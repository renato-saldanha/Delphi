unit Controller.Animal;

interface

uses
  System.JSON, DataSet.Serialize, SysUtils, StrUtils,
  Horse, Services.Animal, Horse.Exception;

procedure Registry;
procedure InserirAnimal(Req: THorseRequest; Res: THorseResponse; Next: TProc);
procedure GetAnimais(Req: THorseRequest; Res: THorseResponse; Next: TProc);
procedure GetAnimalByDescricao(Req: THorseRequest; Res: THorseResponse; Next: TProc);
procedure DeletarAnimal(Req: THorseRequest; Res: THorseResponse; Next: TProc);

implementation


procedure InserirAnimal(Req: THorseRequest; Res: THorseResponse; Next: TProc);
var
  LService: TServiceAnimal;
begin
  LService := TServiceAnimal.Create(nil);

  try
    Res
      .Send<TJSONObject>(LService.Inserir(Req.Body<TJSONObject>).ToJSONObject())
      .Status(THTTPStatus.Created);
  finally
    LService.Free;
  end;
end;

procedure GetAnimais(Req: THorseRequest; Res: THorseResponse; Next: TProc);
var
  LService: TServiceAnimal;
begin
  LService := TServiceAnimal.Create(nil);

  try
    Res
      .Send<TJSONArray>(LService.GetAnimais().ToJSONArray)
      .Status(THTTPStatus.OK);
  finally
    LService.Free;
  end;

end;

procedure DeletarAnimal(Req: THorseRequest; Res: THorseResponse; Next: TProc);
var
  LService: TServiceAnimal;
  LDescricao: String;
begin
  LService := TServiceAnimal.Create(nil);

  try
    LDescricao := Req.Params.Items['name'];
    if LService.GetAnimalByDescricao(LDescricao).IsEmpty then
      raise EHorseException.New
              .Status(THTTPStatus.NotFound)
              .Title('Animal não existe!');
    if LService.Excluir(LDescricao) then
      Res.Status(THTTPStatus.NoContent);
  finally
    LService.Free;
  end;

end;

procedure GetAnimalByDescricao(Req: THorseRequest; Res: THorseResponse; Next: TProc);
var
  LService: TServiceAnimal;
  LDescricao: String;
begin
  LService := TServiceAnimal.Create(nil);

  try
    LDescricao := Req.Params.Items['name'];
    Res
      .Send<TJSONObject>(LService.GetAnimalByDescricao(LDescricao).ToJSONObject)
      .Status(THTTPStatus.OK);
  finally
    LService.Free;
  end;

end;

procedure Registry;
begin
  THorse.Get('/animais', GetAnimais);
  THorse.Get('/animais/:name',  GetAnimalByDescricao);
  THorse.Post('/animais', InserirAnimal);
  THorse.Delete('/animais/:name',  DeletarAnimal);
end;

end.
